plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.1.0'
    id("xyz.jpenilla.run-paper") version "3.0.0"
}

group = 'uz.alex2276564'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "extendedclip"
        url = "https://repo.extendedclip.com/releases/"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "okaeri"
        url = "https://storehouse.okaeri.eu/repository/maven-public/"
    }
    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    compileOnly("me.clip:placeholderapi:2.11.6")

    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.5")

    implementation 'com.google.code.gson:gson:2.13.1'

    compileOnly "net.kyori:adventure-text-minimessage:4.23.0"

    implementation("eu.okaeri:okaeri-configs-yaml-snakeyaml:5.0.13")

    implementation "io.papermc:paperlib:1.0.8"

    implementation("com.github.ben-manes.caffeine:caffeine:3.2.2")

    implementation 'com.tcoded:FoliaLib:0.5.1'
}

// ⚠️ NOTE: Shading is required for legacy compatibility!
// Libraries like Gson may not work correctly on older server versions (1.16.x and below) if not shaded.
// Shadowing ensures that all required dependencies are bundled and isolated within your final plugin JAR.
shadowJar {
    archiveClassifier.set('')

    mergeServiceFiles()

    relocate 'eu.okaeri.configs', 'uz.alex2276564.mmospawnpoint.libs.okaeri.configs'

    relocate 'com.google.gson', 'uz.alex2276564.mmospawnpoint.libs.gson'

    relocate "io.papermc.lib", "uz.alex2276564.mmospawnpoint.libs.paperlib"

    relocate 'com.github.benmanes.caffeine', 'uz.alex2276564.mmospawnpoint.libs.caffeine'

    relocate 'com.tcoded.folialib', 'uz.alex2276564.mmospawnpoint.libs.folialib'
}

build {
    dependsOn shadowJar
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.16.5")
    }
}

def targetJavaVersion = 17

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
